## oh-my-zsh ##

ZSH=$HOME/.oh-my-zsh
ZSH_THEME="agnoster"

alias zshconfig="st ~/.zshrc"
alias ohmyzsh="st ~/.oh-my-zsh"

# Disable auto-update, we're using a manual install
DISABLE_AUTO_UPDATE="true"

plugins=(osx brew git sublime ant mvn autojump postgres)
source $ZSH/oh-my-zsh.sh

## dotfiles ##

DOTFILES=$HOME/.dotfiles

# Use .localrc for things that need to be kept secret
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

if [ -z "$DEFAULT_USER" ]; then
    echo "Please set DEFAULT_USER in your ~/.localrc"
    exit 1
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
